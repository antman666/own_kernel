# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lldb
pkgver=15.0.7
pkgrel=2
pkgdesc="Next generation, high-performance debugger"
arch=('x86_64')
url="https://lldb.llvm.org/"
license=('custom:Apache 2.0 with LLVM Exception')
depends=('llvm-libs' 'clang' 'python' 'python-six')
makedepends=('llvm' 'cmake' 'ninja' 'swig' 'python-sphinx')
_source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
source=($_source_base/lldb-$pkgver.src.tar.xz{,.sig})
sha256sums=('f64d5533661c766a2ff97062af5a0523b9e2cd56aa3d95d9624c418e120d1427'
            'SKIP')
validpgpkeys=('474E22316ABF4785A88C6E8EA2C794A986419D8A'  # Tom Stellard <tstellar@redhat.com>
              'D574BD5D1D0E98895E3BF90044F2485E45D59042') # Tobias Hieta <tobias@hieta.se>

prepare() {
  cd lldb-$pkgver.src
  mkdir build
}

build() {
  cd lldb-$pkgver.src/build

  unset CFLAGS
  unset CXXFLAGS
  export CFLAGS='-march=core2 -mtune=core2 -O3 -fno-plt -fomit-frame-pointer'
  export CXXFLAGS='-march=core2 -mtune=core2 -O3 -fno-plt -fomit-frame-pointer'
  # LDFLAGS=${LDFLAGS/-O3/-O2}
  export CC=clang
  export CXX=clang++
  export AR=llvm-ar
  export AS=llvm-as
  export NM=llvm-nm
  export STRIP=llvm-strip
  export OBJCOPY=llvm-objcopy
  export OBJDUMP=llvm-objdump
  export READELF=llvm-readelf
  export HOSTCC=clang
  export HOSTCXX=clang++
  export HOSTAR=llvm-ar
  export DEBUG_CFLAGS=""
  export DEBUG_CXXFLAGS=""
  export LD=ld.lld
  export CC_LD=lld
  export CXX_LD=lld
  export HOSTLD=ld.lld

  local cmake_args=(
    -G Ninja
    -DCMAKE_C_COMPILER=clang
    -DCMAKE_CXX_COMPILER=clang++
    -DLLVM_USE_LINKER=lld
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_SKIP_RPATH=ON
    -DCLANG_LINK_CLANG_DYLIB=ON
    -DLLDB_USE_SYSTEM_SIX=1
    -DLLVM_ENABLE_SPHINX=ON
    -DLLVM_LINK_LLVM_DYLIB=ON
  )
  cmake .. "${cmake_args[@]}"
  ninja all docs-lldb-man
}

package() {
  cd lldb-$pkgver.src/build

  DESTDIR="$pkgdir" ninja install
  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  install -Dm644 docs/man/lldb.1 "$pkgdir/usr/share/man/man1/lldb.1"

  # Compile Python scripts
  python3 -m compileall "$pkgdir"
  python3 -O -m compileall "$pkgdir"
  python3 -OO -m compileall "$pkgdir"
}

# vim:set ts=2 sw=2 et:
