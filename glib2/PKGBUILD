# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=glib2
pkgver=2.76.3
pkgrel=2
pkgdesc="Low level core library"
url="https://wiki.gnome.org/Projects/GLib"
license=(LGPL)
arch=(x86_64)
depends=(
  libffi
  libsysprof-capture
  pcre2
  util-linux-libs
  zlib
)
makedepends=(
  dbus
  gettext
  git
  gtk-doc
  libelf
  meson
  python
  shared-mime-info
  util-linux
)
checkdepends=(
  desktop-file-utils
  glib2
)
options=(!debug !lto staticlibs)
_commit=69e209764bcfef599a21b32f82f88f1906bf197c  # tags/2.76.3^0
source=("git+https://gitlab.gnome.org/GNOME/glib.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gvdb.git"
        0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch
        0002-glocalfile-Sum-apparent-size-only-for-files-and-syml.patch
        0003-tests-file-Do-not-rely-on-du-bytes-behaviour.patch
        gio-querymodules.hook
        glib-compile-schemas.hook)
sha256sums=('SKIP'
            '6d51eb5856268d79eee01b97a299fa9f99db18b2abb4df56f2ed9e641a09138a'
            '64ae5597dda3cc160fc74be038dbe6267d41b525c0c35da9125fbf0de27f9b25'
            '2a9f9b8235f48e3b7d0f6cfcbc76cd2116c45f28692cac4bd61074c495bd5eb7'
            '92d08db5aa30bda276bc3d718e7ff9dd01dc40dcab45b359182dcc290054e24e')

pkgver() {
  cd glib
  git describe --tags | sed 's/[^-]*-g/r&/;s/-/+/g'
}

prepare() {
  cd glib

  # Suppress noise from glib-compile-schemas.hook
  git apply -3 ../0001-glib-compile-schemas-Remove-noisy-deprecation-warnin.patch

  # fix test suite issues with coreutils >=9.2
  # https://gitlab.gnome.org/GNOME/glib/-/merge_requests/3358
  git apply -3 ../0002-glocalfile-Sum-apparent-size-only-for-files-and-syml.patch
  git apply -3 ../0003-tests-file-Do-not-rely-on-du-bytes-behaviour.patch

  git submodule init
  git submodule set-url subprojects/gvdb "$srcdir/gvdb"
  git -c protocol.file.allow=always submodule update
}

build() {
  local meson_options=(
    --default-library both
    -D glib_debug=disabled
    -D gtk_doc=false
    -D man=false
    -D selinux=disabled
    -D sysprof=enabled
    -D b_ndebug=true
    -D b_pie=false
    -D tests=flase
    -D c_std=gnu18
    -D cpp_std=gnu++2a
    --buildtype=release
  )

  # Produce more debug info: GLib has a lot of useful macros
  #CFLAGS+=" -g3"
  #CXXFLAGS+=" -g3"

  # use fat LTO objects for static libraries
  # CFLAGS+=" -ffat-lto-objects"
  # CXXFLAGS+=" -ffat-lto-objects"
  export CFLAGS+=" -DG_DISABLE_CAST_CHECKS"
  export CXXFLAGS+=" -DG_DISABLE_CAST_CHECKS"

  arch-meson glib build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --no-suite flaky --no-suite slow --print-errorlogs
}

package_glib2() {
  depends+=(
    libffi.so
    libmount.so
  )
  provides+=(libg{lib,io,module,object,thread}-2.0.so)
  optdepends=(
    'gvfs: most gio functionality'
    'libelf: gresource inspection tool'
    'python: gdbus-codegen, glib-genmarshal, glib-mkenums, gtester-report'
  )

  meson install -C build --destdir "$pkgdir"

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
  touch "$pkgdir/usr/lib/gio/modules/.keep"

  python -m compileall -d /usr/share/glib-2.0/codegen \
    "$pkgdir/usr/share/glib-2.0/codegen"
  python -O -m compileall -d /usr/share/glib-2.0/codegen \
    "$pkgdir/usr/share/glib-2.0/codegen"

  # Split docs
  # mkdir -p docs/usr/share
  # mv {"$pkgdir",docs}/usr/share/gtk-doc
}

# package_glib2-docs() {
#   pkgdesc+=" - documentation"
#   depends=()
#   license+=(custom)

#   mv -t "$pkgdir" docs/*
#   install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 glib/docs/reference/COPYING
# }

# vim:set sw=2 sts=-1 et:
