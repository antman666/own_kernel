# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc

pkgname=binutils
pkgver=2.38
pkgrel=5
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(x86_64)
url='https://www.gnu.org/software/binutils/'
license=(GPL)
groups=(base-devel)
depends=(glibc zlib elfutils)
makedepends=(elfutils git)
conflicts=(binutils-multilib)
replaces=(binutils-multilib)
options=(staticlibs)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig}
        binutils-no-config-h-check.patch
        binutils-revert-PLT-elision.patch
        binutils-special-sections-in-groups.patch
        binutils-fix-testsuite-failures.patch
        binutils-gold-mismatched-section-flags.patch
        binutils-CVE-2019-1010204.patch
        binutils-gold-warn-unsupported.patch
        binutils-use-long-long.patch
        binutils-gold-i386-gnu-property-notes.patch
        binutils-autoconf-version.patch
        binutils-readelf-other-sym-info.patch
        binutils-libtool-no-rpath.patch
        binutils-section-type.patch
        binutils-2.38-fix-build.patch
        binutils-do-not-use-debuginfod.patch
        binutils-gas-loc-view.patch
        fix-incorrect-undefined-symbol.patch
        binutils-link-following.patch
        binutils-x86-non-canonical-references.patch
        binutils-update-linker-manual.patch
)
sha256sums=('e316477a914f567eccc34d5d29785b8b0f5a10208d36bbacedcc39048ecfe024'
            'SKIP'
            'c842f958c26c56b868ca7e9a56012490aef4ff29a4956c06ab422955758a9af6'
            'cf9fd37b33332866a2cebb8e090b51fa6ed5ac3defbff8787d8953056b5b3ba9'
            '143f53ebe64cf7640d8bce44446995c2d1b393fd3c5a4dbd0a82c24352e0e8f4'
            '49e01577e2f95926441642d2e798bcfc4ea08a9c83e4d28bbb9d563d569b7ef3'
            '1939d75dbaa43a917e3388a6b9382ae0ba200b6f65b95979e226281be507394c'
            '199b4044d06f8daa2197a7a4d03cf8b2a63f2e7b8493e709c6a016c1e3f902d1'
            '2d430b66f84a19c154725ec535280c493be1d34f1a90f95208dce02fecd0e4e4'
            '73af103882e23936b5423e5756a6edf485ef327091e0b9aaa2a51be55764e98e'
            '72ba8f1a0c9d8ec0f443f11d1a3bea8fff5d7b6ff6cdbd0fd02ded747959503d'
            '915c02b0e5df3c6849e170304a2ee16f5741b74fdb6e5ad755a38364d06afc62'
            'e6ffa1b8452ae74293523140a9eb767b3b30b73d9e466709fcf8c478a05bd455'
            '995f16f70b4b2be12f0c8dec4bdc5e074c831f35751b99de2c2f47053621745f'
            'e60f198bb49f29dfca2355e6ca2fae24c2a933a55ab567fedfa55ca5ae57aad4'
            '2ac235c2218e36c3d3154f1d7923fe1518601de0d4714aa1754277674737b8ea'
            '0e661641bbcf15ac8d3d3dbff802ccd9c475309a8fcdcc0294e2d3e2001252d2'
            '796ed0a124c7f93cd6f6f4063c915b455cf999fd619d8faa0f593c71ede22564'
            '31954c5a617a9c985a991ffb12be74f60de0d062cb63627629baa1e844439c79'
            'fd33b2f8cac7561cecf3fdbb5a50fd2f2dfa6420516cbe57c47784a06fa16bf6')
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)

prepare() {
  [[ ! -d binutils-gdb ]] && ln -s binutils-$pkgver binutils-gdb
  mkdir -p binutils-build

  cd binutils-gdb

  ##############################
  #                            #
  #    From Fedora binutils    #
  #                            #
  ##############################

  # Purpose:  Disables the check in the BFD library's bfd.h header file that
  #           config.h has been included before the bfd.h header.  See BZ
  #           #845084 for more details.
  # Lifetime: Permanent - but it should not be.  The bfd.h header defines
  #           various types that are dependent upon configuration options, so
  #           the order of inclusion is important.
  # FIXME:    It would be better if the packages using the bfd.h header were
  #           fixed so that they do include the header files in the correct
  #           order.
  patch -p1 -i ../binutils-no-config-h-check.patch

  # Purpose:  Disable an x86/x86_64 optimization that moves functions from the
  #           PLT into the GOTPLT for faster access.  This optimization is
  #           problematic for tools that want to intercept PLT entries, such
  #           as ltrace and LD_AUDIT.  See BZs 1452111 and 1333481.
  # Lifetime: Permanent.  But it should not be.
  # FIXME:    Replace with a configure time option.
  patch -p1 -i ../binutils-revert-PLT-elision.patch

  # Purpose:  Allow OS specific sections in section groups.
  # Lifetime: Fixed in 2.39 (maybe)
  patch -p1 -i ../binutils-special-sections-in-groups.patch

  # Purpose:  Fix linker testsuite failures.
  # Lifetime: Fixed in 2.39 (maybe)
  patch -p1 -i ../binutils-fix-testsuite-failures.patch

  # Purpose:  Stop gold from aborting when input sections with the same name
  #            have different flags.
  # Lifetime: Fixed in 2.39 (maybe)
  patch -p1 -i ../binutils-gold-mismatched-section-flags.patch

  # Purpose:  Add a check to the GOLD linker for a corrupt input file
  #            with a fuzzed section offset.
  # Lifetime: Fixed in 2.39 (maybe)
  patch -p1 -i ../binutils-CVE-2019-1010204.patch

  # Purpose:  Change the gold configuration script to only warn about
  #            unsupported targets.  This allows the binutils to be built with
  #            BPF support enabled.
  # Lifetime: Permanent.
  patch -p1 -i ../binutils-gold-warn-unsupported.patch

  # Purpose:  Use the "unsigned long long" type for pointers on hosts where
  #           long is a 32-bit type but pointers are a 64-bit type.  Necessary
  #           because users expect to be able to install both the i686- and
  #           x86_64 versions of binutils-devel on the same machine, so they
  #           need to identical versions of the bfd.h header file.
  # Lifetime: Permanent.
  patch -p1 -i ../binutils-use-long-long.patch

  # Purpose:  Enable the creation of .note.gnu.property sections by the GOLD
  #            linker for x86 binaries.
  # Lifetime: Fixed in 2.39 maybe
  patch -p1 -i ../binutils-gold-i386-gnu-property-notes.patch

  # Purpose: Allow the binutils to be configured with any (recent) version of
  #          autoconf.
  # Lifetime: Fixed in 2.39 (maybe ?)
  patch -p1 -i ../binutils-autoconf-version.patch

  # Purpose:  Changes readelf so that when it displays extra information about
  #           a symbol, this information is placed at the end of the line.
  # Lifetime: Permanent.
  # FIXME:    The proper fix would be to update the scripts that are expecting
  #           a fixed output from readelf.  But it seems that some of them are
  #           no longer being maintained.
  patch -p1 -i ../binutils-readelf-other-sym-info.patch

  # Purpose:  Simplify the evaluation of assembler loc view expression chains.
  # Lifetime: Fixed in 2.39
  patch -p1 -i ../binutils-gas-loc-view.patch

  # Purpose:  Stop libtool from inserting useless runpaths into binaries.
  # Lifetime: Who knows.
  patch -p1 -i ../binutils-libtool-no-rpath.patch

  # Purpose:  Add support for specifying section types in linker scripts.
  # Lifetime: Fixed in 2.39
  patch -p1 -i ../binutils-section-type.patch

  # Purpose:  Add an option to disable access to debuginfod servers.
  # Lifetime: Fixed in 2.39
  patch -p1 -i ../binutils-do-not-use-debuginfod.patch

  # fix incorrect "undefined reference" errors
  # Purpose:  Keep indirect symbol from IR if referenced from shared object.
  # Lifetime: Fixed in 2.39
  # https://sourceware.org/bugzilla/show_bug.cgi?id=28879
  patch -p1 -i ../fix-incorrect-undefined-symbol.patch

  # Purpose:  Stop readelf and objdump from unnecessarily following links.
  # Lifetime: Fixed in 2.39
  patch -p1 -i  ../binutils-link-following.patch

  # Purpose:  x86 linker: Disallow invalid relocations against protected symbols.
  # Lifetime: Fixed in 2.39
  patch -p1 -i  ../binutils-x86-non-canonical-references.patch

  # Purpose:  Change ld man page so that it says that --enable-new-dtags is the default.
  # Lifetime: Permanent
  patch -p1 -i  ../binutils-update-linker-manual.patch

  # My patch to fix build 2.38
  patch -p1 -i ../binutils-2.38-fix-build.patch
  
  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

}

build() {
  cd binutils-build

  "$srcdir/binutils-gdb/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-gold \
    --enable-ld=default \
    --enable-lto \
    --enable-plugins \
    --enable-relro \
    --enable-64-bit-bfd \
    --enable-targets="x86_64-pep,bpf-unknown-none" \
    --enable-threads \
    --enable-new-dtags \
    --enable-multilib \
    --disable-rpath \
    --disable-gdb \
    --disable-werror \
    --enable-deterministic-archives=no \
    --enable-generate-build-notes=no \
    --disable-cet \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make configure-host
  make tooldir=/usr
}

check() {
  cd binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # ignore failures in gold testsuite...
  make -k LDFLAGS="" check || true
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
