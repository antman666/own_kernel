# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.1.1
pkgrel=1
pkgdesc='A Modern Linker'
arch=('x86_64')
url="https://github.com/rui314/mold"
license=('AGPL3')
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb')
makedepends=('git' 'clang')
_commit='c1af9c97e763a1d521889191a83b27bd6c43b351' # v1.1.1
source=("$pkgname::git+$url.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # use /usr/lib instead of /usr/libexec
  sed -i "s/libexec/lib/" Makefile
}

build() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1
}

check() {
  cd "$pkgname"

  # temporarily remove failing tests
  for failing_test in exception; do
    rm -vf "test/elf/$failing_test.sh"
  done

  make \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    check
}

package() {
  make \
    -C "$pkgname" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    DESTDIR="$pkgdir" \
    install
}
